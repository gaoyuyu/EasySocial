apply plugin: 'com.android.application'

def releaseTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    aaptOptions.cruncherEnabled = false
    aaptOptions.useNewCruncher = false

    defaultConfig {
        applicationId "com.gaoyy.easysocial"
        minSdkVersion 19
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
    }
    //执行lint检查，有任何的错误或者警告提示，都会终止构建，我们可以将其关掉。
    lintOptions {
        abortOnError false
    }

    //签名
    signingConfigs {
        debug {

        }
        release {// 自己的签名文件
            storeFile file("D:/AndroidStudioProjects/签名文件/gaoyy_signature.jks")
            storePassword "1qa2ws"
            keyAlias "gaoyy_android"
            keyPassword "1qa2ws"
        }
    }

    buildTypes {
        debug {
            // 显示Log
            buildConfigField "boolean", "LOG_DEBUG", "true"
            versionNameSuffix "-debug"
            minifyEnabled false
            zipAlignEnabled false
            shrinkResources false
            signingConfig signingConfigs.debug
        }
        release {
            // 不显示Log
            buildConfigField "boolean", "LOG_DEBUG", "false"
            //混淆
            minifyEnabled true
            //Zipalign优化
            zipAlignEnabled true

            // 移除无用的resource文件
            shrinkResources true
            //前一部分代表系统默认的android程序的混淆文件，该文件已经包含了基本的混淆声明，后一个文件是自己的定义混淆文件
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            //签名
            signingConfig signingConfigs.release

            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def outputFile = output.outputFile
                    if (outputFile != null && outputFile.name.endsWith('.apk')) {
                        // 输出apk名称为app_v1.0_2015-xx-xx_xxxx.apk
                        def fileName = "app_v${defaultConfig.versionName}_${releaseTime()}.apk"
//                        def fileName = "app_v${defaultConfig.versionName}_${variant.productFlavors[0].name}.apk"
                        output.outputFile = new File(outputFile.parent, fileName)
                    }
                }
            }
        }
    }
}


dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile('com.android.support:appcompat-v7:23.4.0') {
        force = true;
    }
    compile('com.android.support:support-v4:23.4.0') {
        force = true;
    }
    compile('com.android.support:design:23.4.0') {
        force = true;
    }
    compile 'com.readystatesoftware.systembartint:systembartint:1.0.4'
    compile 'com.balysv:material-ripple:1.0.2'
    compile 'me.drakeet.materialdialog:library:1.2.8'
    compile 'com.rengwuxian.materialedittext:library:2.1.4'
    compile 'com.github.siyamed:android-shape-imageview:0.9.3'
    compile 'com.squareup.okhttp3:okhttp:3.2.0'
    compile 'com.facebook.fresco:fresco:0.9.0'
    compile 'com.android.support:cardview-v7:23.4.0'
    compile 'com.google.code.gson:gson:2.2.4'
    compile 'com.pnikosis:materialish-progress:1.7'
    compile 'com.android.support:recyclerview-v7:23.4.0'
    compile 'me.relex:photodraweeview:1.0.0'
    //If you can not start app, you cancel the annotation
    //如果app无法启动,请取消下面的注释
    //Other ABIs: optional
    //    compile 'tv.danmaku.ijk.media:ijkplayer-armv5:0.6.0'
    compile 'tv.danmaku.ijk.media:ijkplayer-arm64:0.6.0'
    compile 'tv.danmaku.ijk.media:ijkplayer-x86:0.6.0'
    compile 'tv.danmaku.ijk.media:ijkplayer-x86_64:0.6.0'
    //ExoPlayer as IMediaPlayer: optional, experimental
    //compile 'tv.danmaku.ijk.media:ijkplayer-exo:0.6.0'
    compile 'com.android.support.constraint:constraint-layout:1.0.0-alpha1'
    testCompile 'junit:junit:4.12'
    compile project(':jcvideoplayer-lib')
    compile project(':DroidPlugin')

    //LeakCanary
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.4-beta2'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.4-beta2'
    testCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.4-beta2'
}
